apply plugin: 'java'

sourceCompatibility = 1.5
targetCompatibility = 1.5

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/libs-release" }
    }
    ext {
        appName = "jsame4k-applet"
        appVersion = "1.0.1"
        finalName = "${appName}-${appVersion}"
        proguardBase = "${appName}-proguard-base"
    }
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:5.+"
    }
}

jar {
    baseName = proguardBase
    exclude("jsame-applet.html")
}

task proguard(type: proguard.gradle.ProGuardTask) {
    description 'Generate optimized/obfuscated jar'
    injars "${buildDir}/libs/${proguardBase}.jar"
    outjars "${buildDir}/libs/${finalName}.jar"
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    keep 'public class S'
}

task deleteProguardBase(type: Delete, dependsOn: proguard) {
    description 'Delete ProGuard base jar'
    delete "${buildDir}/libs/${proguardBase}.jar"
}

task checkFileSize(dependsOn: deleteProguardBase) {
    doLast {
        File file = new File("${buildDir}/libs/${finalName}.jar")
        if (file.size() > 4096) {
            throw new GradleException('File size must not exceed 4096 bytes.')
        }
    }
}

task copyAppletHtml(type: Copy) {
    description 'Copy applet template for generated jar'
    from "src/main/resources"
    into "${buildDir}/libs"
    rename 'jsame-applet.html', "${finalName}.html"
    filter { line -> line.contains('jsame4k-applet.jar') ? line.replaceAll('jsame4k-applet.jar', "${finalName}.jar") : line }
}

build.dependsOn checkFileSize, copyAppletHtml
