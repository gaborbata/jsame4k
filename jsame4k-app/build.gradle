apply plugin: 'java'

sourceCompatibility = 1.5
targetCompatibility = 1.5

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "http://repo.spring.io/libs-release" }
    }
    ext {
        appName = "jsame4k-app"
        appVersion = "1.0.0"
        finalName = "${appName}-${appVersion}"
        proguardBase = "${appName}-proguard-base"
    }
    dependencies {
        classpath "net.sf.proguard:proguard-gradle:5.+"
    }
}

jar {
    baseName = proguardBase
    manifest { attributes 'Main-Class': 'S' }
}

task proguard(type: proguard.gradle.ProGuardTask) {
    description 'Generate optimized/obfuscated jar'
    injars "${buildDir}/libs/${proguardBase}.jar"
    outjars "${buildDir}/libs/${finalName}.jar"
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    keep 'public class S {public static void main(java.lang.String[]);}'
}

task deleteProguardBase(type: Delete, dependsOn: proguard) {
    description 'Delete ProGuard base jar'
    delete "${buildDir}/libs/${proguardBase}.jar"
}

task checkFileSize(dependsOn: deleteProguardBase) << {
    description 'Check 4k file size limit'
    File file = new File("${buildDir}/libs/${finalName}.jar")
    if (file.size() > 4096) {
        throw new GradleException('File size must not exceed 4096 bytes.')
    }
}

build.dependsOn checkFileSize
